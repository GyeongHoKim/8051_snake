	ORG	8000H
DATAOUT	EQU	0FFF0H
DATAIN	EQU	0FFF1H
DLED	EQU	0FFC1H;7-SEGMENT RIGHT
ALED0	EQU	0FFC2H;7-SEGMENT MIDDLE
ALED1	EQU	0FFC3H;7-SEGMENT LEFT

REP_COUNT	EQU	5

VSEC	EQU	30H
VMIN	EQU	31H
VHOUR	EQU	32H
VBUF	EQU	33H;TEMP

	ORG	8000H
	MOV	SP,#60H

	MOV	VHOUR,#80H
	MOV	VMIN,#51H
	MOV	VSEC,#00H
	CALL	DISPLAY

MAIN:	CALL	FINDKEYCODE
	JB	ACC.4,ERR
	MOV	VBUF,A
	CALL	SHIFT
	CALL	DISPLAY
	CALL	BOUNCE
	JMP	MAIN

ERR:	CALL	ERROR
	JMP	MAIN

BOUNCE:	CALL	DELAY
RELOAD:	MOV	A, #0
	CALL	SUBKEY
	CPL	A
	JNZ	RELOAD

	CALL	DELAY
	RET

ERROR:	MOV	R4, #REP_COUNT

ERR_1:	MOV	VSEC, #0
	MOV	VMIN, #0
	MOV	VHOUR, #0
	CALL	DISPLAY

	MOV	R3, #10

FIRST:	CALL	DELAY
	DJNZ	R3, FIRST

	MOV	VSEC, #0FFH
	MOV	VMIN, #0FFH
	MOV	VHOUR, #0FFH
	CALL	DISPLAY

	MOV	R3, #10
SECOND:	CALL	DELAY
	DJNZ	R3, SECOND

	DJNZ	R4, ERR_1
	RET

DELAY:	MOV	R0, #020H

REPEAT:	MOV	R1, #0FFH
	DJNZ	R1, $
	DJNZ	R0, REPEAT
	RET

SHIFT:	MOV	A, VHOUR
	SWAP	A
	MOV	VHOUR, A

	MOV	A, VMIN
	SWAP	A
	MOV	VMIN, A

	MOV	A, VSEC
	SWAP	A
	MOV	VSEC, A

	MOV	A, VMIN
	MOV	R0, #VHOUR
	XCHD	A, @R0
	MOV	VMIN, A

	MOV	A, VSEC
	MOV	R0, #VMIN
	XCHD	A, @R0
	MOV	VSEC, A

	MOV	A, VBUF
	MOV	R0, #VSEC
	XCHD	A, @R0
	RET

FINDKEYCODE:
	PUSH	PSW
	SETB	PSW.4
	SETB	PSW.3

INITIAL:
	MOV	R1, #00H
	MOV	A, #11101111B
	SETB	C

COLSCAN:
	MOV	R0, A
	INC	R1
	CALL	SUBKEY
	
	CJNE	A, #0FFH, RSCAN
	MOV	A, R0
	SETB	C
	RRC	A
	JNC	INITIAL
	JMP	COLSCAN

RSCAN:	MOV	R2, #00H
ROWSCAN:
	RRC	A
	JNC	MATRIX
	INC	R2
	JMP	ROWSCAN

MATRIX:	MOV	A, R2
	MOV	B, #05H
	MUL	AB
	ADD	A, R1
	CALL	INDEX
	POP	PSW
	RET

SUBKEY:	MOV	DPTR, #DATAOUT
	MOVX	@DPTR, A
	MOV	DPTR, #DATAIN
	MOVX	A, @DPTR
	RET

DISPLAY:
	MOV	DPTR, #DLED
	MOV	A, VSEC
	MOVX	@DPTR, A

	MOV	DPTR, #ALED0
	MOV	A, VMIN
	MOVX	@DPTR, A

	MOV	DPTR, #ALED1
	MOV	A, VHOUR
	MOVX	@DPTR, A
	RET

RWKEY	EQU	10H
COMMA	EQU	11H
PERIOD	EQU	12H
GO	EQU	13H
REG	EQU	14H
CD	EQU	15H
INCR	EQU	16H
ST	EQU	17H
RST	EQU	18H

INDEX:	MOVC	A,@A+PC
	RET

KEYBASE:
	DB ST
	DB INCR
	DB CD
	DB REG
	DB GO
	DB 0CH
	DB 0DH
	DB 0EH
	DB 0FH
	DB COMMA
	DB 08H
	DB 09H
	DB 0AH
	DB 0BH
	DB PERIOD
	DB 04H
	DB 05H
	DB 06H
	DB 07H
	DB RWKEY
	DB 00H
	DB 01H
	DB 02H
	DB 03H
	END